import org.jetbrains.kotlin.gradle.plugin.KotlinPluginWrapperKt
import tasks.CheckReadmeTask

buildscript {
    repositories {
        maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlinx/maven' }
        gradlePluginPortal()
        google()

        KotlinCommunity.addDevRepositoryIfEnabled(delegate, project)
    }

    dependencies {
        classpath(libs.kotlinx.teamInfraGradlePlugin)

        String kotlinVersion = providers.gradleProperty("kotlin_version").orNull
        if (kotlinVersion != null && !kotlinVersion.isBlank()) {
            // In addition to overriding the Kotlin version in the Version Catalog,
            // also enforce the KGP version using a dependency constraint.
            // Constraints are stricter than Version Catalog.
            constraints {
                classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
            }
        }
        classpath "com.android.tools.build:gradle:8.2.2"
    }
}

plugins {
    id("base")
    alias(libs.plugins.kotlin.multiplatform) apply false
    alias(libs.plugins.kotlinx.binaryCompatibilityValidator)
}

apply plugin: 'kotlinx.team.infra'

infra {
    teamcity {
        libraryStagingRepoDescription = project.name
    }

    publishing {
        include(":kotlinx-benchmark-runtime")

        libraryRepoUrl = "https://github.com/Kotlin/kotlinx-benchmark"

        if (project.findProperty("publication_repository") == "sonatype") {
            sonatype {}
        }
    }
}

// https://youtrack.jetbrains.com/issue/KT-48410
repositories {
    mavenCentral()
}

// region Workarounds for https://github.com/gradle/gradle/issues/22335
tasks.register("apiDump") {
    it.dependsOn(gradle.includedBuild("plugin").task(":apiDump"))
}

afterEvaluate {
    gradle.includedBuilds.forEach { included ->
        project(":kotlinx-benchmark-runtime").tasks.named("publishToMavenLocal") { dependsOn(included.task(":publishToMavenLocal")) }
    }
}
//endregion

String currentKgpVersion = KotlinPluginWrapperKt.getKotlinPluginVersion(project)
logger.info("Using Kotlin Gradle Plugin ${currentKgpVersion}")

String kotlinVersionOverride = providers.gradleProperty("kotlin_version").getOrNull()

if (kotlinVersionOverride != null) {
    String versionCatalogKotlinVersion = libs.versions.kotlin.get()
    if (kotlinVersionOverride != versionCatalogKotlinVersion) {
        throw new IllegalStateException("Kotlin version in Version Catalog was not overridden. Expected:$kotlinVersionOverride, actual:$versionCatalogKotlinVersion.")
    }
    if (kotlinVersionOverride != currentKgpVersion) {
        throw new IllegalStateException("Kotlin Gradle Plugin version was not overridden. Expected:$kotlinVersionOverride, actual:$currentKgpVersion.")
    }
}

allprojects {
    repositories {
        KotlinCommunity.addDevRepositoryIfEnabled(delegate, project)
    }
}

apiValidation {
    ignoredProjects += [
            "examples",
            "java",
            "kotlin",
            "kotlin-kts",
            "kotlin-multiplatform",
            "integration",
    ]

    nonPublicMarkers += ["kotlinx.benchmark.internal.KotlinxBenchmarkRuntimeInternalApi"]

    klib {
        it.enabled = true
    }
}

tasks.register("checkReadme", CheckReadmeTask) {
    minSupportedGradleVersion = libs.versions.minSupportedGradle
    readme = file("README.md")
}

tasks.check {
    dependsOn(tasks.named("checkReadme"))
}
