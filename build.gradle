buildscript {
    repositories {
        maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlinx/maven' }
        gradlePluginPortal()
        maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev' }
        mavenLocal()

        KotlinCommunity.addDevRepositoryIfEnabled(delegate, project)
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "kotlinx.team:kotlinx.team.infra:$infra_version"
    }
}

plugins {
    id 'org.jetbrains.kotlinx.binary-compatibility-validator' version '0.13.2'
}

apply plugin: 'kotlinx.team.infra'

infra {
    teamcity {
        libraryStagingRepoDescription = project.name
        overrideBuildNumber = rootProject.properties["build_snapshot_up"] != "true"
    }

    publishing {
        include(":kotlinx-benchmark-runtime")

        libraryRepoUrl = "https://github.com/Kotlin/kotlinx-benchmark"

        if (project.findProperty("publication_repository") == "sonatype") {
            sonatype {}
        }
    }
}

// https://youtrack.jetbrains.com/issue/KT-48410
repositories {
    mavenCentral()
    maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev' }
    mavenLocal()
}

afterEvaluate {
    gradle.includedBuilds.forEach { included ->
        project(":kotlinx-benchmark-runtime").tasks.named("publishToMavenLocal") { dependsOn(included.task(":publishToMavenLocal")) }
    }
}

allprojects {
    logger.info("Using Kotlin $kotlin_version for project $it")
    repositories {
        KotlinCommunity.addDevRepositoryIfEnabled(delegate, project)
        maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev' }
        mavenLocal()
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask).configureEach {
        compilerOptions {
            freeCompilerArgs.add("-Xpartial-linkage-loglevel=ERROR")
        }
    }
}