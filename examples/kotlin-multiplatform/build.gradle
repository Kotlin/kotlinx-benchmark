plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.kotlin.plugin.allopen' version "1.5.30"
    id 'org.jetbrains.kotlinx.benchmark' version "0.3.1"
}

// how to apply plugin to a specific source set?
allOpen {
    annotation("org.openjdk.jmh.annotations.State")
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }

    js {
        nodejs { }
    }

    macosX64 { }
    linuxX64 { }
    mingwX64 { }

    sourceSets.all {
        languageSettings {
            progressiveMode = true
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation project(":kotlinx-benchmark-runtime")
            }
        }

        jvmMain {}

        jsMain {}

        nativeMain {
            dependsOn(commonMain)

            macosX64Main.dependsOn(it)
            linuxX64Main.dependsOn(it)
            mingwX64Main.dependsOn(it)
        }
    }
}

// Configure benchmark
benchmark {
    configurations {
        main { // --> jvmBenchmark/jsBenchmark + benchmark
            iterations = 5 // number of iterations
            iterationTime = 300
            iterationTimeUnit = "ms"
            advanced("forks", 3)
        }

        params {
            iterations = 5 // number of iterations
            iterationTime = 300
            iterationTimeUnit = "ms"
            include("ParamBenchmark")
            param("data", 5, 1, 8)
            param("unused", 6, 9)
        }

        fast { // --> jvmFastBenchmark
            include("Common")
            exclude("long")
            iterations = 5
            iterationTime = 300 // time in ms per iteration
            iterationTimeUnit = "ms" // time in ms per iteration
            advanced("forks", 1)
            advanced("nativeFork", "perIteration")
            advanced("nativeGCCollectMode", "iteration")
        }

        csv {
            include("Common")
            exclude("long")
            iterations = 1
            iterationTime = 300
            iterationTimeUnit = "ms"
            reportFormat = "csv" // csv report format
        }
    }

    // Setup configurations
    targets {
        // This one matches compilation base name, e.g. 'jvm', 'jvmTest', etc
        register("jvm") {
            jmhVersion = "1.21"
        }
        register("js")
        register("macosX64")
        register("linuxX64")
        register("mingwX64")
    }
}

