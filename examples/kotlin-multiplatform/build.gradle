plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.kotlin.plugin.allopen' version "1.6.0"
    id 'org.jetbrains.kotlinx.benchmark' version "0.4.1"
}

// how to apply plugin to a specific source set?
allOpen {
    annotation("org.openjdk.jmh.annotations.State")
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }

    js {
        nodejs { }
    }

    macosX64 { }
    macosArm64 { }
    linuxX64 { }
    mingwX64 { }

    sourceSets.all {
        languageSettings {
            progressiveMode = true
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation project(":kotlinx-benchmark-runtime")
            }
        }

        jvmMain {}

        jsMain {}

        nativeMain {
            dependsOn(commonMain)

            macosX64Main.dependsOn(it)
            macosArm64Main.dependsOn(it)
            linuxX64Main.dependsOn(it)
            mingwX64Main.dependsOn(it)
        }
    }
}

// Configure benchmark
benchmark {
    configurations {
        main { // --> jvmBenchmark, jsBenchmark, <native target>Benchmark, benchmark
            iterations = 5 // number of iterations
            iterationTime = 300
            iterationTimeUnit = "ms"
            advanced("jvmForks", 3)
        }

        params {
            iterations = 5 // number of iterations
            iterationTime = 300
            iterationTimeUnit = "ms"
            include("ParamBenchmark")
            param("data", 5, 1, 8)
            param("unused", 6, 9)
        }

        fast { // --> jvmFastBenchmark, jsFastBenchmark, <native target>FastBenchmark, fastBenchmark
            include("Common")
            exclude("long")
            iterations = 5
            iterationTime = 300 // time in ms per iteration
            iterationTimeUnit = "ms" // time in ms per iteration
            advanced("nativeGCAfterIteration", true)
        }

        csv {
            include("Common")
            exclude("long")
            iterations = 1
            iterationTime = 300
            iterationTimeUnit = "ms"
            reportFormat = "csv" // csv report format
        }

        fork {
            include("CommonBenchmark")
            iterations = 5
            iterationTime = 300
            iterationTimeUnit = "ms"
            advanced("jvmForks", "definedByJmh") // see README.md for possible "jvmForks" values
            advanced("nativeFork", "perIteration") // see README.md for possible "nativeFork" values
        }
    }

    // Setup configurations
    targets {
        // This one matches compilation base name, e.g. 'jvm', 'jvmTest', etc
        register("jvm") {
            jmhVersion = "1.21"
        }
        register("js")
        register("macosX64")
        register("macosArm64")
        register("linuxX64")
        register("mingwX64")
    }
}

