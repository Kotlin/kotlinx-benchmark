import kotlinx.benchmark.gradle.JsBenchmarksExecutor
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.kotlin.plugin.allopen' version "1.9.21"
    id 'org.jetbrains.kotlinx.benchmark'
    id 'com.android.library'
}

// how to apply plugin to a specific source set?
allOpen {
    annotation("org.openjdk.jmh.annotations.State")
}

android {
    compileSdk 33
    namespace = "org.jetbrains.kotlinx.examples"

    defaultConfig {
        minSdk = 29
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
//            isMinifyEnabled = false
//            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

// Could not determine the dependencies of task ':examples:kotlin-multiplatform:extractReleaseAnnotations'.
// > Could not resolve all task dependencies for configuration ':examples:kotlin-multiplatform:detachedConfiguration1'.
//    > Could not find com.android.tools.lint:lint-gradle:31.2.2.
repositories {
    google()
}

kotlin {
    androidTarget {
        // Android target does not have any compilations
        println("android compilations: ${compilations.size()}")
    }
    jvm {
        compilations.create('benchmark') { associateWith(compilations.main) }
    }
    js(IR) {
        nodejs()
        compilations.create("defaultExecutor") { associateWith(compilations.main) }
        compilations.create("builtInExecutor") { associateWith(compilations.main) }
    }
    wasm('wasmJs') { nodejs() }

    // Native targets
    macosX64()
    macosArm64()
    linuxX64()
    mingwX64()

    applyDefaultHierarchyTemplate()

    targets.configureEach {
        compilations.configureEach {
            kotlinOptions.allWarningsAsErrors = true
        }
    }

    sourceSets.configureEach {
        languageSettings {
            progressiveMode = true
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation project(":kotlinx-benchmark-runtime")
            }
        }

        jvmMain {}

        wasmJsMain {}

        jsMain {
            jsDefaultExecutor.dependsOn(it)
            jsBuiltInExecutor.dependsOn(it)
        }

        nativeMain {}

        androidMain {
            kotlin.setSrcDirs(["src/androidMain/kotlin"])
        }

        forEach {
            // Android target has 3 source sets: androidMain, androidUnitTest, and androidInstrumentedTest
            println("SourceSet: $it")
            println(it.name)
            println(it.kotlin)
            println(it.kotlin.srcDirs)
            println(it.kotlin.sourceDirectories)
        }

    }
}

// Configure benchmark
benchmark {
    configurations {
        main { // --> jvmBenchmark, jsBenchmark, <native target>Benchmark, benchmark
            iterations = 5 // number of iterations
            iterationTime = 300
            iterationTimeUnit = "ms"
            advanced("jvmForks", 3)
            advanced("jsUseBridge", true)
        }

        params {
            iterations = 5 // number of iterations
            iterationTime = 300
            iterationTimeUnit = "ms"
            include("ParamBenchmark")
            param("data", 5, 1, 8)
            param("unused", 6, 9)
        }

        fast { // --> jvmFastBenchmark, jsFastBenchmark, <native target>FastBenchmark, fastBenchmark
            include("Common")
            exclude("long")
            iterations = 5
            iterationTime = 300 // time in ms per iteration
            iterationTimeUnit = "ms" // time in ms per iteration
            advanced("nativeGCAfterIteration", true)
        }

        csv {
            include("Common")
            exclude("long")
            iterations = 1
            iterationTime = 300
            iterationTimeUnit = "ms"
            reportFormat = "csv" // csv report format
        }

        fork {
            include("CommonBenchmark")
            iterations = 5
            iterationTime = 300
            iterationTimeUnit = "ms"
            advanced("jvmForks", "definedByJmh") // see README.md for possible "jvmForks" values
            advanced("nativeFork", "perIteration") // see README.md for possible "nativeFork" values
        }
    }

    // Setup configurations
    targets {
        // This one matches target name, e.g. 'jvm', 'js',
        // and registers its 'main' compilation, so 'jvm' registers 'jvmMain'
        register("jvm") {
            jmhVersion = "1.21"
        }
        // This one matches source set name, e.g. 'jvmMain', 'jvmTest', etc
        // and register the corresponding compilation (here the 'benchmark' compilation declared in the 'jvm' target)
        register("jvmBenchmark") {
            jmhVersion = "1.21"
        }
        register("jsDefaultExecutor")
        register("jsBuiltInExecutor") {
            jsBenchmarksExecutor = JsBenchmarksExecutor.BuiltIn
        }
        register("wasmJs")

        // Native targets
        register("macosX64")
        register("macosArm64")
        register("linuxX64")
        register("mingwX64")
        register("android")
    }
}

// Node.js with canary v8 that supports recent Wasm GC changes
rootProject.extensions.findByType(org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootExtension.class).with {
    nodeVersion = "21.0.0-v8-canary202309167e82ab1fa2"
    nodeDownloadBaseUrl = "https://nodejs.org/download/v8-canary"
}

// Drop this when node js version become stable
rootProject.tasks.withType(org.jetbrains.kotlin.gradle.targets.js.npm.tasks.KotlinNpmInstallTask.class).configureEach {
    args.add("--ignore-engines")
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_1_8)
    }
}