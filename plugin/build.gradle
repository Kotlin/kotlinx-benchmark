buildscript {
    repositories {
    	maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        mavenCentral()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
    }
    dependencies {
        classpath "kotlinx.team:kotlinx.team.infra:$infra_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

}

plugins {
    id 'java-gradle-plugin'
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'kotlinx.team.infra'

infra {
    publishing {
        include(":")

        bintray {
            organization = 'kotlin'
            repository = 'kotlinx'
            library = 'kotlinx.benchmark'

            username = findProperty('bintrayUser')
            password = findProperty('bintrayApiKey')
        }
    }
}

repositories {
	maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    mavenCentral()
    jcenter()
    gradlePluginPortal()
}

gradlePlugin {
    plugins {
        benchmarkPlugin {
            id = "kotlinx.benchmark"
            implementationClass = "kotlinx.benchmark.gradle.BenchmarksPlugin"
        }
    }
}

sourceSets {
    main {
        kotlin.srcDirs = ['main/src']
        java.srcDirs = ['main/src']
        resources.srcDirs = ['main/resources']
    }
    test {
        kotlin.srcDirs = ['test/src']
        java.srcDirs = ['test/src']
        resources.srcDirs = ['test/resources']
    }
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version

    compile 'com.squareup:kotlinpoet:1.3.0'
    compile "org.openjdk.jmh:jmh-core:$jmhVersion"

    compile group: 'org.jetbrains.kotlin', name: 'kotlin-util-klib-metadata', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-util-klib', version: kotlin_version
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-util-io', version: kotlin_version
    
    compileOnly group: 'org.jetbrains.kotlin.multiplatform', name: 'org.jetbrains.kotlin.multiplatform.gradle.plugin', version: kotlin_version
    compileOnly "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
    compileOnly "org.openjdk.jmh:jmh-generator-bytecode:$jmhVersion" // used in worker

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation gradleTestKit()
    testImplementation "junit:junit:$junit_version"
    
/*
    // This is needed for test to get it on classpath and run
    runtime "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    runtime "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
*/
}
