buildscript {
    def loadPropFromRootProject = { String prop ->
        ext.set(prop, rootProject.properties[prop])
        if (ext.get(prop) == null) {
            def filename = projectDir.getAbsoluteFile().toPath().parent.resolve("gradle.properties").toString()
            def fis = new FileInputStream(filename)
            def properties = new Properties()
            properties.load(fis)
            ext.set(prop, properties.getProperty(prop))
        }
    }
    ["kotlin_version", "kotlin_language_version", "kotlin_api_version"].each {
        loadPropFromRootProject(it)
    }
    ext.kotlinDevUrl = rootProject.properties["kotlin_repo_url"]
    repositories {
        maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlinx/maven' }
        mavenCentral()
        maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev' }
        mavenLocal()
        if (kotlinDevUrl != null) {
            maven { url = kotlinDevUrl }
        }
    }
    dependencies {
        classpath "kotlinx.team:kotlinx.team.infra:$infra_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project.ext.kotlin_version"
    }

}

plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.21.0'
    id 'org.jetbrains.kotlinx.binary-compatibility-validator' version '0.13.2'
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'kotlinx.team.infra'

infra {
    teamcity {
        libraryStagingRepoDescription = project.name
        overrideBuildNumber = rootProject.properties["build_snapshot_up"] != "true"
    }

    publishing {
        include(":")

        libraryRepoUrl = "https://github.com/Kotlin/kotlinx-benchmark"
    }
}

logger.info("Using Kotlin ${project.ext.kotlin_version} for project ${project.name}")

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev' }
    mavenLocal()

    if (kotlinDevUrl != null) {
        maven { url = kotlinDevUrl }
    }
}

pluginBundle {
    website = 'https://github.com/Kotlin/kotlinx-benchmark'
    vcsUrl = 'https://github.com/Kotlin/kotlinx-benchmark.git'
    tags = ['benchmarking', 'multiplatform', 'kotlin']
}

gradlePlugin {
    plugins {
        benchmarkPlugin {
            id = "org.jetbrains.kotlinx.benchmark"
            implementationClass = "kotlinx.benchmark.gradle.BenchmarksPlugin"
            displayName = "Gradle plugin for benchmarking"
            description = "Toolkit for running benchmarks for multiplatform Kotlin code."
        }
    }
}

sourceSets {
    main {
        kotlin.srcDirs = ['main/src']
        java.srcDirs = ['main/src']
        resources.srcDirs = ['main/resources']
    }
    test {
        kotlin.srcDirs = ['test/src']
        java.srcDirs = ['test/src']
        resources.srcDirs = ['test/resources']
    }
    configureEach {
        languageSettings.languageVersion = project.ext.kotlin_language_version
        languageSettings.apiVersion = project.ext.kotlin_api_version
    }
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        languageVersion = kotlin_language_version
        apiVersion = kotlin_api_version
    }

    compilerOptions {
        freeCompilerArgs.add("-Xpartial-linkage-loglevel=ERROR")
    }
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: project.ext.kotlin_version

    implementation 'com.squareup:kotlinpoet:1.3.0'

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-util-klib-metadata', version: project.ext.kotlin_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-util-klib', version: project.ext.kotlin_version
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-util-io', version: project.ext.kotlin_version

    compileOnly group: 'org.jetbrains.kotlin.multiplatform', name: 'org.jetbrains.kotlin.multiplatform.gradle.plugin', version: project.ext.kotlin_version
    compileOnly group: "org.jetbrains.kotlin", name: "kotlin-compiler-embeddable", version: project.ext.kotlin_version
    compileOnly "org.openjdk.jmh:jmh-generator-bytecode:$jmhVersion" // used in worker
}

tasks.register("overridePluginVersion") {
    description = "Overrides BenchmarksPlugin.PLUGIN_VERSION during release builds"
    onlyIf {
        project.findProperty("releaseVersion") != null
    }
    doLast {
        def benchmarksPluginFile = "${projectDir}/main/src/kotlinx/benchmark/gradle/BenchmarksPlugin.kt"
        def releaseVersion = project.findProperty("releaseVersion")
        ant.replaceregexp(
                file: benchmarksPluginFile,
                match: "const val PLUGIN_VERSION = \"[\\d.]+-SNAPSHOT\"",
                replace: "const val PLUGIN_VERSION = \"$releaseVersion\"",
                encoding: "UTF-8"
        )
    }
}

tasks.compileKotlin.dependsOn overridePluginVersion

if (project.findProperty("publication_repository") == "space") {
    // publish to Space repository
    publishing {
        repositories {
            maven {
                name = "space"
                url = "https://maven.pkg.jetbrains.space/kotlin/p/kotlinx/dev"
                credentials {
                    username = project.findProperty("space.user")
                    password = project.findProperty("space.token")
                }
            }
        }
    }
}