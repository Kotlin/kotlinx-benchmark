pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url 'https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev' }
        mavenLocal()

        if (settings.hasProperty("kotlin_repo_url") && settings.kotlin_repo_url != null) {
            maven { url = settings.kotlin_repo_url }
        }
    }
}

dependencyResolutionManagement {
    versionCatalogs {
        create("libs") {
            from(files("../gradle/libs.versions.toml"))

            String kotlinVersion = providers.gradleProperty("kotlin_version").orNull
            if (kotlinVersion != null && !kotlinVersion.isBlank()) {
                // Override the default Kotlin version.
                // The only intended use-case is for testing dev Kotlin builds using kotlinx-benchmark.
                // The Kotlin version should not be overridden during regular development.
                version("kotlin", kotlinVersion)
            }
        }
    }
}

rootProject.name = 'kotlinx-benchmark-plugin'
