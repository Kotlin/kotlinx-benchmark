plugins {
    id 'org.jetbrains.kotlin.multiplatform'
}

repositories {
    mavenCentral()
}

kotlin {
    infra {
        target('linuxX64')
        target('mingwX64')

        common("darwin") {
            target("macosX64")
            target("macosArm64")
            target("iosX64")
            target("iosArm64")
            target("iosArm32")
            target("iosSimulatorArm64")
            target("watchosArm32")
            target("watchosArm64")
            target("watchosX86")
            target("watchosX64")
            target("watchosSimulatorArm64")
            target("tvosArm64")
            target("tvosX64")
            target("tvosSimulatorArm64")
        }
    }

    jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = '1.8'
            }
        }
    }

    js {
        nodejs()
    }

    sourceSets.all {
        kotlin.srcDirs = ["$it.name/src"]
        resources.srcDirs = ["$it.name/resources"]
        languageSettings {
            progressiveMode = true
        }
    }

    sourceSets {
        commonMain {
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                compileOnly "org.openjdk.jmh:jmh-core:$jmhVersion"
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
                implementation "org.openjdk.jmh:jmh-core:$jmhVersion"
            }
        }
        jsMain {}
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        nativeMain {
            dependsOn(commonMain)
        }
    }
}


// publish to Space repository
publishing {
    repositories {
        maven {
            name = "space"
            url = "https://maven.pkg.jetbrains.space/kotlin/p/kotlinx/dev"
            credentials {
                username = project.findProperty("space.user")
                password = project.findProperty("space.token")
            }
        }
    }
}
