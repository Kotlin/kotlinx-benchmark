import org.jetbrains.kotlin.konan.target.KonanTarget
import org.jetbrains.kotlin.konan.target.HostManager

kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '1.8'
        }
    }

    js {
        nodejs()
    }

    if (HostManager.hostIsLinux) linuxX64('native')
    if (HostManager.hostIsMingw) mingwX64('native')
    if (HostManager.host == KonanTarget.MACOS_X64.INSTANCE) macosX64('native')
    if (HostManager.host == KonanTarget.MACOS_ARM64.INSTANCE) macosArm64('native')

    sourceSets {
        commonMain {
            dependencies {
                implementation(benchmarkRuntimeMetadata)
            }
        }
        jvmMain {
            dependencies {
                implementation(benchmarkRuntimeJvm)
            }
        }
        jsMain {
            dependencies {
                implementation(benchmarkRuntimeJs)
            }
        }
        nativeMain {
            dependsOn(commonMain)
            dependencies {
                implementation(benchmarkRuntimeNative)
            }
        }
    }
}

benchmark {
    targets {
        register("jvm")
        register("js")
        register("native")
    }
}
