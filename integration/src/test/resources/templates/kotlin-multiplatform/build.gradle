import org.jetbrains.kotlin.konan.target.KonanTarget
import org.jetbrains.kotlin.konan.target.HostManager

kotlin {
    jvm {
        compilations.create('benchmark') { associateWith(compilations.main) }
    }
    js('jsIr', IR) { nodejs() }
    wasm { d8() }

    if (HostManager.hostIsLinux) linuxX64('native')
    if (HostManager.hostIsMingw) mingwX64('native')
    if (HostManager.host == KonanTarget.MACOS_X64.INSTANCE) macosX64('native')
    if (HostManager.host == KonanTarget.MACOS_ARM64.INSTANCE) macosArm64('native')

    sourceSets {
        commonMain {
            dependencies {
                implementation(benchmarkRuntimeMetadata)
            }
        }
        jvmMain {
            dependencies {
                implementation(benchmarkRuntimeJvm)
            }
        }
        jvmBenchmark {
            dependsOn(jvmMain)
            dependencies {
                implementation(benchmarkRuntimeJvm)
            }
        }
        jsIrMain {
            dependencies {
                implementation(benchmarkRuntimeJsIr)
            }
        }
        wasmMain {
            dependencies {
                implementation(benchmarkRuntimeWasm)
            }
        }
        nativeMain {
            dependsOn(commonMain)
            dependencies {
                implementation(benchmarkRuntimeNative)
            }
        }
    }
}

repositories {
    def kotlinDevUrl = rootProject.properties["kotlin_repo_url"]
    if (kotlinDevUrl != null) {
        maven { url = kotlinDevUrl }
    }
}

benchmark {
    targets {
        register("jvm")
        register("jsIr")
        register("wasm")
        register("native")
    }
}
